################################################
# YOU SHOULD NOT NEED TO MODIFY ANYTHING BELOW #
################################################

# Auto-detect SOFA_DIR if not specified
isEmpty(SOFA_DIR) {
  win32 {
    SOFA_DIR = $$system(echo %CD%)
  }
  else:macx {
    SOFA_DIR = $$(PWD)
  }
  else {
    SOFA_DIR = $$system(pwd)
  }
}

DLLDESTDIR = $$SOFA_DIR/bin

# Auto-detect Qt 4.x
# The QT variable was introduced in Qt 4
!isEmpty(QT): DEFINES += SOFA_QT4

!include( sofa-local.cfg ) {
  # Copy sofa-default.cfg to sofa-local.cfg and modify it to change local options
  include( sofa-default.cfg )
}

CONFIG -= qt

win32{
	CONFIG -= flat # preserve directory structure in files lists
	contains (CONFIG, debug, debug|release) {
		 CONFIGDEBUG=debug
	}
	contains (CONFIG, release, debug|release) {
		 CONFIGDEBUG=release
	}

	# It's for windows project where we have output streams in a console.

	# Config for Visual C++ 6
	win32-msvc{
		CONFIGPROJECT=console vc6 thread
		contains (CONFIGDEBUG, debug) {
			LIBSDIRECTORY=win32/DebugVC6
		}
		contains (CONFIGDEBUG, release) {
			LIBSDIRECTORY=win32/ReleaseVC6
		}
	}
	
	# Config for Visual C++ 7
	win32-msvc.net{
		CONFIGPROJECT=console vc7 thread
		contains (CONFIGDEBUG, debug) {
			LIBSDIRECTORY=win32/DebugVC7
			QMAKE_LFLAGS += /NODEFAULTLIB:libcd /NODEFAULTLIB:MSVCRT	
		}
		contains (CONFIGDEBUG, release) {
			LIBSDIRECTORY=win32/ReleaseVC7
			QMAKE_LFLAGS += /NODEFAULTLIB:libc /NODEFAULTLIB:MSVCRTD
		}
		QMAKE_CXXFLAGS_DEBUG = -wd4675 -wd4250 -GR -GX -MDd # -O1 -Og -Ob2 -GA -Ot -MDd # -G7 
		QMAKE_CXXFLAGS_RELEASE = -wd4675 -wd4250 -GR -GX -Ox -Og -Ob2 -GA -Ot -MD # -G7
	}
	
	# Config for Visual C++ 8
	win32-msvc2005{
		CONFIGPROJECT=console vc7 thread
		contains (CONFIGDEBUG, debug) {
			LIBSDIRECTORY=win32/DebugVC8
			QMAKE_LFLAGS += /NODEFAULTLIB:libcd /NODEFAULTLIB:MSVCRT	
		}
		contains (CONFIGDEBUG, release) {
			LIBSDIRECTORY=win32/ReleaseVC8
			QMAKE_LFLAGS += /NODEFAULTLIB:libc /NODEFAULTLIB:MSVCRTD
		}
		QMAKE_CXXFLAGS_DEBUG = -wd4996 -wd4250 -GR -EHsc -MDd  -Od -ZI -Gm # -O1 -Og -Ob2 -GA -Ot -MDd # -G7 
		QMAKE_CXXFLAGS_RELEASE = -wd4996 -wd4250 -GR -EHsc -Ox -Og -Ob2 -GA -Ot -MD # -G7
	}

	# No support for DLL yet
	CONFIGLIBRARIES=staticlib

	#CONFIGLIBRARIES=dll
	#MSVCPROJ_LIBOPTIONS=/IMPLIB:$(TargetDir)$(TargetName).lib

	# libs has no suffix
	LIBSUFFIX=
}
unix {
	macx {
		QMAKE_LFLAGS += -L/System/Library/Frameworks/
		
		contains (CONFIG, debug) {
		 	CONFIGDEBUG=debug
		}
		contains (CONFIG, release) {
			CONFIGDEBUG=release
		}
		LIBSDIRECTORY=macx

		CONFIGLIBRARIES=staticlib 

		contains (CONFIGDEBUG, debug) {
			LIBSUFFIX = d
		}
	}
 	
	!macx {
		contains (CONFIG, debug) {
		 	CONFIGDEBUG=debug
		}
		contains (CONFIG, release) {
			CONFIGDEBUG=release
		}
		LIBSDIRECTORY=linux

		# Use shared library
		CONFIGLIBRARIES=dynamiclib

		contains (CONFIGDEBUG, debug) {
			LIBSUFFIX = d
		}
	}
}

contains (CONFIGDEBUG, release) {
	DEFINES += NDEBUG
}

contains (CONFIGDEBUG, debug) {
	SUFFIX = d
}

OBJECTS_DIR = OBJ/$$CONFIGDEBUG

CONFIGLIBRARIES+= \
                  warn_on \
                  stl \
                  rtti \
                  thread

CONFIGPROJECT += warn_on
CONFIGPROJECTGUI = $$CONFIGPROJECT
CONFIGPROJECTCMD = $$CONFIGPROJECT
CONFIGPROJECTCMD += console

INCLUDEPATH *= \
               $$SOFA_DIR/include \
               $$SOFA_DIR/extlibs \
               $$SOFA_DIR/framework \
               $$SOFA_DIR/modules \
               $$SOFA_DIR/applications

DEPENDPATH  *= \
               $$SOFA_DIR/framework \
               $$SOFA_DIR/modules \
               $$SOFA_DIR/applications

QMAKE_LIBDIR = $$SOFA_DIR/lib/$$LIBSDIRECTORY 

!macx: QMAKE_LIBDIR += $$SOFA_DIR/lib/$$LIBSDIRECTORY/../Common

# List of libs to link with

SOFA_EXT_LIBS *=  -lNewMAT$$LIBSUFFIX -lSLC$$LIBSUFFIX
win32 {
	SOFA_EXT_LIBS *= -llibxml2 -lGLaux -lglut32 -lcomctl32 -lopengl32 -lglu32 -lAdvAPI32 -lUser32 -lShell32 -lGdi32 -lWSock32 -lWS2_32 -lOle32
}

unix {
	macx : SOFA_EXT_LIBS += -lpthread -lxml2 -framework OpenGL -framework GLUT
	!macx : SOFA_EXT_LIBS *= -L/usr/X11R6/lib -lglut -lGL -lGLU -lpthread -lxml2 -lz
}


########################################################################
#  LibPNG
########################################################################
contains(DEFINES,SOFA_HAVE_PNG){
	win32 {
		SOFA_EXT_LIBS *= -llibpng -lzlib
	}
	unix {
		macx : SOFA_EXT_LIBS *= -lpng -lz 
		!macx : SOFA_EXT_LIBS *= -lpng -lz 
	}
}

########################################################################
#  GLEW
########################################################################
contains(DEFINES,SOFA_HAVE_GLEW){
	win32 {
		SOFA_EXT_LIBS *= -lglew32
	}
	unix {
		macx : SOFA_EXT_LIBS *= -lGLEW
		!macx : SOFA_EXT_LIBS *= -lGLEW
	}
}

SOFA_FRAMEWORK_LIBS *= -lsofahelper$$LIBSUFFIX -lsofadefaulttype$$LIBSUFFIX -lsofacore$$LIBSUFFIX

SOFA_MODULES_LIBS *= -lsofacomponent$$LIBSUFFIX -lsofasimulation$$LIBSUFFIX

SOFA_GUI_LIBS *= -lsofagui$$LIBSUFFIX -lsofaguimain$$LIBSUFFIX

########################################################################
# GLUT
########################################################################
contains (DEFINES, SOFA_GUI_GLUT) {
	SOFA_GUI_LIBS *= -lsofaguiglut$$LIBSUFFIX
}

########################################################################
# FLTK
########################################################################
contains (DEFINES, SOFA_GUI_FLTK) {
	SOFA_GUI_LIBS *= -lsofaguifltk$$LIBSUFFIX
	win32 {
		SOFA_EXT_LIBS *= -lfltk -lfltkgl
	}
	unix {
		SOFA_EXT_LIBS *= -lfltk_gl -lfltk
	}
}

########################################################################
# QT
########################################################################
contains (DEFINES, SOFA_GUI_QTVIEWER) {
	SOFA_GUI_LIBS *= -lsofaguiqt$$LIBSUFFIX
	macx: LIBS += -framework QtXml -framework QtSql -framework QtNetwork
	CONFIGPROJECTGUI += qt
	contains (DEFINES, SOFA_QT4) {	
	  QT += opengl qt3support
	}
	else {	
	  QT += opengl	
	}
	INCLUDEPATH += $$SOFA_DIR/extlibs/qwt
	SOFA_EXT_LIBS *= -lqwt$${LIBSUFFIX}
}

########################################################################
# QGLVIEWER
########################################################################
contains (DEFINES, SOFA_GUI_QGLVIEWER) {
	SOFA_GUI_LIBS *= -lsofaguiqt$$LIBSUFFIX
	macx: {
		LIBS += -framework QtXml 
		INCLUDEPATH += /Library/Frameworks/QtXml.framework/Versions/4/Headers 
	}
	CONFIGPROJECTGUI += qt
	
	contains (DEFINES, SOFA_QT4) {	
	  QT += opengl qt3support
	}
	else {
	  QT += opengl	
	}
	unix:SOFA_EXT_LIBS *= -lQGLViewer
	win32:SOFA_EXT_LIBS *= -lQGLViewer2
	INCLUDEPATH += $$SOFA_DIR/extlibs/qwt
	SOFA_EXT_LIBS *= -lqwt$${LIBSUFFIX}
}

########################################################################
# QTOGREVIEWER
########################################################################
contains (DEFINES, SOFA_GUI_QTOGREVIEWER) {
	SOFA_GUI_LIBS *= -lsofaguiqt$$LIBSUFFIX
	macx: {
		LIBS += -framework Ogre -framework CoreFoundation
		INCLUDEPATH += /Library/Frameworks/Ogre.framework/Headers/
	}

	CONFIGPROJECTGUI += qt

	contains (DEFINES, SOFA_QT4) {
	  QT += opengl qt3support
	}
	else {
	  QT += opengl
	}

	win32 {
		INCLUDEPATH += $(OGRE_HOME)/include
		QMAKE_LIBDIR += $(OGRE_HOME)/lib

		LIBS += OgreMain.lib  
	}
	INCLUDEPATH += $$SOFA_DIR/extlibs/qwt
	SOFA_EXT_LIBS *= -lqwt$${LIBSUFFIX}
}

########################################################################
# PML
########################################################################
contains(DEFINES,SOFA_PML){
	INCLUDEPATH += $$SOFA_DIR/extlibs/PML
	INCLUDEPATH += $$SOFA_DIR/extlibs/PML/PhysicalProperties
	INCLUDEPATH += $$SOFA_DIR/extlibs/LML
	SOFA_EXT_LIBS *= -lphysicalmodel$$LIBSUFFIX -lload$$LIBSUFFIX
	SOFA_MODULES_LIBS *= -lsofapml$$LIBSUFFIX
}

########################################################################
# CUDA
########################################################################
contains (DEFINES, SOFA_GPU_CUDA) {
	SOFA_MODULES_LIBS *= -lsofagpucuda$$LIBSUFFIX
	SOFA_EXT_LIBS *= -lcudart
	cuda.input = CUDA_SOURCES
	QMAKE_EXTRA_UNIX_COMPILERS *= cuda
	win32 {
		INCLUDEPATH += $(CUDA_INC_DIR)
		QMAKE_LIBDIR += $(CUDA_LIB_DIR)
		SOFA_EXT_LIBS *= -lcudart

		cuda.output = $${OBJECTS_DIR}/${QMAKE_FILE_BASE}_cuda.obj
                cuda.commands = $${CUDA_BIN_DIR}/nvcc.exe	$${join(QMAKE_CXXFLAGS,",")}" $${join(INCLUDEPATH,'" -I "','-I "','"')} " ${QMAKE_FILE_NAME} -o ${QMAKE_FILE_OUT}
}
	unix {
		# auto-detect CUDA path
		CUDA_DIR = $${system(which nvcc | sed 's,/bin/nvcc$,,')}
		INCLUDEPATH += $${CUDA_DIR}/include
		QMAKE_LIBDIR += $${CUDA_DIR}/lib
		SOFA_EXT_LIBS *= -lcudart

		cuda.output = ${OBJECTS_DIR}${QMAKE_FILE_BASE}_cuda.obj
		cuda.commands = nvcc -c -Xcompiler $${join(QMAKE_CXXFLAGS,",")} "$$join(INCLUDEPATH,'" -I "','-I "','"')" ${QMAKE_FILE_NAME} -o ${QMAKE_FILE_OUT}
		cuda.depends = nvcc -M -Xcompiler $${join(QMAKE_CXXFLAGS,",")} "$$join(INCLUDEPATH,'" -I "','-I "','"')" ${QMAKE_FILE_NAME} | tail +2 | sed "s,^ *,,; s/ \\\\$//" | paste -s -d " " | sed "s,/[^ ]*,,g" | tee dep-${QMAKE_FILE_NAME}
	}
}


########################################################################
#  FLOWVR
########################################################################
contains(DEFINES,SOFA_HAVE_FLOWVR){
	FLOWVR = $(FLOWVR_PREFIX)
	QMAKE_CXXFLAGS += `pkg-config --cflags flowvr-mod flowvr-ftl flowvr-render`
	QMAKE_LDFLAGS += `pkg-config --libs flowvr-mod flowvr-ftl flowvr-render`
	QMAKE_LFLAGS_DEBUG+= `pkg-config --libs flowvr-mod flowvr-ftl flowvr-render`
	QMAKE_LFLAGS_RELEASE+= `pkg-config --libs flowvr-mod flowvr-ftl flowvr-render`
}
else {
	SOFA_EXT_LIBS += -lminiFlowVR
	INCLUDEPATH *= $$SOFA_DIR/extlibs/miniFlowVR/include
	DEFINES *= MINI_FLOWVR
}

########################################################################
#  MKL
########################################################################
contains(DEFINES,SOFA_HAVE_MKL){
	INCLUDEPATH += $$SOFA_DIR/extlibs/MKL
	INCLUDEPATH += "$$MKL_PATH/include"
	QMAKE_LIBDIR += "$$MKL_PATH/ia32/lib"
	SOFA_EXT_LIBS *= -lmkl_c -lmkl_lapack -llibguide40
}

########################################################################

SOFA_LIBS = $${SOFA_MODULES_LIBS} $${SOFA_FRAMEWORK_LIBS} $${SOFA_EXT_LIBS}

# default destination 
DESTDIR = $${SOFA_DIR}/lib/$${LIBSDIRECTORY}





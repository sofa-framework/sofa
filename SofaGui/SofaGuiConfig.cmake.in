# CMake package configuration file for SofaGui

@PACKAGE_INIT@

set(SOFAGUI_TARGETS @SOFAGUI_TARGETS@)

set(SOFAGUI_HAVE_SOFAHEADLESSRECORDER @SOFAGUI_HAVE_SOFAHEADLESSRECORDER@)
set(SOFAGUI_HAVE_SOFAGUIQT @SOFAGUI_HAVE_SOFAGUIQT@)
set(SOFAGUIQT_HAVE_QTVIEWER @SOFAGUIQT_HAVE_QTVIEWER@)
set(SOFAGUIQT_HAVE_QGLVIEWER @SOFAGUIQT_HAVE_QGLVIEWER@)
set(SOFAGUIQT_HAVE_QT5_CHARTS @SOFAGUIQT_HAVE_QT5_CHARTS@)
set(SOFAGUIQT_HAVE_QWT @SOFAGUIQT_HAVE_QWT@)
set(SOFAGUIQT_HAVE_NODEEDITOR @SOFAGUIQT_HAVE_NODEEDITOR@)
set(SOFAGUIQT_HAVE_FFMPEG_EXEC @SOFAGUIQT_HAVE_FFMPEG_EXEC@)

# Find dependencies
find_package(SofaGeneral REQUIRED)
find_package(SofaMisc REQUIRED)
find_package(SofaComponentAll REQUIRED) # Needed by SofaGuiCommon

if(SOFAGUI_HAVE_SOFAGUIQT)
    if(SOFAGUIQT_HAVE_QTVIEWER)
        find_package(Qt5 QUIET REQUIRED Core Gui OpenGL WebEngine WebEngineWidgets)
        if(SOFAGUIQT_HAVE_QT5_CHARTS)
            find_package(Qt5 QUIET REQUIRED Charts)
        endif()
    endif()
    if(SOFAGUIQT_HAVE_QGLVIEWER)
        find_package(QGLViewer QUIET REQUIRED)
    endif()
    if(SOFAGUIQT_HAVE_QWT)
        find_package(Qwt QUIET REQUIRED)
    endif()
endif()


if(SOFAGUIQT_HAVE_NODEEDITOR)
    find_package(NodeEditor QUIET REQUIRED)
endif()

foreach(target ${SOFAGUI_TARGETS})
    if(NOT TARGET ${target})
        include("${CMAKE_CURRENT_LIST_DIR}/SofaGuiTargets.cmake")
        break()
    endif()
endforeach()

check_required_components(${SOFAGUI_TARGETS})

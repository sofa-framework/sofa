# CMake package configuration file for the @PROJECT_NAME@ module

@PACKAGE_GUARD@
@PACKAGE_INIT@

set(SOFA_GUI_QT_HAVE_QT5 @SOFA_GUI_QT_HAVE_QT5@)
set(SOFA_GUI_QT_HAVE_QT6 @SOFA_GUI_QT_HAVE_QT6@)

set(SOFA_GUI_QT_ENABLE_QTVIEWER @SOFA_GUI_QT_ENABLE_QTVIEWER@)
set(SOFA_GUI_QT_ENABLE_QGLVIEWER @SOFA_GUI_QT_ENABLE_QGLVIEWER@)
set(SOFA_GUI_QT_ENABLE_NATIVE_MENU @SOFA_GUI_QT_ENABLE_NATIVE_MENU@)
set(SOFA_GUI_QT_ENABLE_VSYNC @SOFA_GUI_QT_ENABLE_VSYNC@)

set(SOFA_GUI_QT_HAVE_QT5_CHARTS @SOFA_GUI_QT_HAVE_QT5_CHARTS@)
set(SOFA_GUI_QT_HAVE_QT5_WEBENGINE @SOFA_GUI_QT_HAVE_QT5_WEBENGINE@)
set(SOFA_GUI_QT_HAVE_NODEEDITOR @SOFA_GUI_QT_HAVE_NODEEDITOR@)
set(SOFA_GUI_QT_HAVE_TINYXML @SOFA_GUI_QT_HAVE_TINYXML@)

set(SOFA_GUI_QT_HAVE_QT6_CHARTS @SOFA_GUI_QT_HAVE_QT6_CHARTS@)
set(SOFA_GUI_QT_HAVE_QT6_WEBENGINECORE @SOFA_GUI_QT_HAVE_QT6_WEBENGINECORE@)

# This is needed to be able to find plugins/SofaGuiQt/lib/cmake/QGLViewer
list(APPEND CMAKE_PREFIX_PATH "${CMAKE_CURRENT_LIST_DIR}/..")

find_package(Sofa.GUI.Common QUIET REQUIRED)
find_package(Sofa.Component.Visual QUIET REQUIRED)
find_package(Sofa.Component.SceneUtility QUIET REQUIRED)

if(SOFA_GUI_QT_HAVE_QT5)
    find_package(Qt5 QUIET REQUIRED Core Gui OpenGL)
    if(SOFA_GUI_QT_HAVE_QT5_CHARTS)
        find_package(Qt5 QUIET REQUIRED Charts)
    endif()
    if(SOFA_GUI_QT_HAVE_QT5_WEBENGINE)
        find_package(Qt5 QUIET REQUIRED WebEngine WebEngineWidgets)
    endif()
    if(SOFA_GUI_QT_ENABLE_QGLVIEWER)
        find_package(QGLViewer QUIET REQUIRED)
    endif()
elseif(SOFA_GUI_QT_HAVE_QT6)
    find_package(Qt6 COMPONENTS Widgets OpenGLWidgets QUIET REQUIRED)
    if(SOFA_GUI_QT_HAVE_QT6_CHARTS)
        find_package(Qt6 QUIET REQUIRED Charts)
    endif()
    if(SOFA_GUI_QT_HAVE_QT6_WEBENGINECORE)
        find_package(Qt6 QUIET REQUIRED Positioning WebChannel WebEngineCore WebEngineWidgets)
    endif()
    if(SOFA_GUI_QT_ENABLE_QGLVIEWER)
        find_package(QGLViewer QUIET REQUIRED)
    endif()
endif()

if(SOFA_GUI_QT_HAVE_NODEEDITOR)
    find_package(NodeEditor QUIET REQUIRED)
endif()


if(NOT TARGET @PROJECT_NAME@)
    include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")
endif()

check_required_components(@PROJECT_NAME@)

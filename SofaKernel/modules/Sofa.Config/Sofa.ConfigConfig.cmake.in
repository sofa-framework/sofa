# CMake package configuration file for the @PROJECT_NAME@ module

@PACKAGE_GUARD@
@PACKAGE_INIT@

set(Sofa_VERSION @Sofa_VERSION@)

get_filename_component(SOFA_ROOT "${CMAKE_CURRENT_LIST_DIR}/../../.." ABSOLUTE)

# Add CMAKE_CURRENT_LIST_DIR to CMAKE_MODULE_PATH (if not already done)
# Needed by: include(SofaMacros)
list(FIND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}" HAS_SOFAFRAMEWORK_CMAKE_MODULE_PATH)
if(HAS_SOFAFRAMEWORK_CMAKE_MODULE_PATH EQUAL -1)
    list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})
endif()

list(FIND CMAKE_PREFIX_PATH "${SOFA_ROOT}/plugins" HAS_PLUGINS_CMAKE_PREFIX_PATH)
if(HAS_PLUGINS_CMAKE_PREFIX_PATH EQUAL -1)
    list(APPEND CMAKE_PREFIX_PATH "${CMAKE_CURRENT_LIST_DIR}/../../../plugins")
endif()

list(APPEND CMAKE_INCLUDE_PATH "${SOFA_ROOT}/include/extlibs/WinDepPack")
list(APPEND CMAKE_MODULE_PATH "${SOFA_ROOT}/lib/cmake/Modules")

# Help RELOCATABLE plugins to resolve their dependencies.
# See SofaMacrosInstall.cmake for usage of this property.
define_property(TARGET
    PROPERTY "RELOCATABLE_INSTALL_DIR"
    BRIEF_DOCS "Install directory of RELOCATABLE target"
    FULL_DOCS "Install directory of RELOCATABLE target"
    )

include(SofaMacros)

set(SOFA_WITH_OPENGL "@SOFA_WITH_OPENGL@")
if(SOFA_WITH_OPENGL)
    set(SOFA_NO_OPENGL FALSE) # Deprecated, use SOFA_WITH_OPENGL instead
else()
    set(SOFA_NO_OPENGL TRUE) # Deprecated, use SOFA_WITH_OPENGL instead
endif()
set(SOFA_USE_MASK "@SOFA_USE_MASK@")

set(SOFA_WITH_DEVTOOLS "@SOFA_WITH_DEVTOOLS@")
set(SOFA_WITH_THREADING "@SOFA_WITH_THREADING@")
set(SOFA_WITH_DEPRECATED_COMPONENTS "@SOFA_WITH_DEPRECATED_COMPONENTS@")

if(NOT TARGET @PROJECT_NAME@)
    include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")
endif()

check_required_components(@PROJECT_NAME@)

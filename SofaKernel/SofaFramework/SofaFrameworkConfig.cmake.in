# CMake package configuration file for SofaFramework
cmake_minimum_required(VERSION 3.12)

@PACKAGE_GUARD@
@PACKAGE_INIT@

set(SOFAFRAMEWORK_TARGETS @SOFAFRAMEWORK_TARGETS@)

set(SOFAHELPER_HAVE_BOOST "@SOFAHELPER_HAVE_BOOST@")
set(SOFAHELPER_HAVE_BOOST_SYSTEM "@SOFAHELPER_HAVE_BOOST_SYSTEM@")
set(SOFAHELPER_HAVE_BOOST_FILESYSTEM "@SOFAHELPER_HAVE_BOOST_FILESYSTEM@")
set(SOFAHELPER_HAVE_BOOST_PROGRAM_OPTIONS "@SOFAHELPER_HAVE_BOOST_PROGRAM_OPTIONS@")
set(SOFAHELPER_HAVE_BOOST_THREAD "@SOFAHELPER_HAVE_BOOST_THREAD@")
set(SOFAHELPER_HAVE_OPENGL "@SOFAHELPER_HAVE_OPENGL@")
set(SOFAHELPER_HAVE_GLEW "@SOFAHELPER_HAVE_GLEW@")
set(SOFAHELPER_HAVE_GTEST "@SOFAHELPER_HAVE_GTEST@")

# Find dependencies
find_package(Boost QUIET REQUIRED system filesystem program_options)
if(SOFAHELPER_HAVE_BOOST_THREAD)
    find_package(Boost QUIET REQUIRED thread)
endif()
if(SOFAHELPER_HAVE_OPENGL)
    find_package(OpenGL QUIET REQUIRED)
endif()
if(SOFAHELPER_HAVE_GLEW)
    find_package(GLEW QUIET REQUIRED)
endif()
if(SOFAHELPER_HAVE_GTEST)
    find_package(GTest CONFIG QUIET REQUIRED)
endif()

# Eigen3 is required by SofaDefaultType and SofaHelper
find_package(Eigen3 QUIET REQUIRED)

foreach(target ${SOFAFRAMEWORK_TARGETS})
    if(NOT TARGET ${target})
        include("${CMAKE_CURRENT_LIST_DIR}/SofaFrameworkTargets.cmake")
        break()
    endif()
endforeach()
if(NOT TARGET @PROJECT_NAME@)
    include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")
endif()

# This need to be AFTER the inclusion of SofaFrameworkTargets.cmake since Sofa.GL is dependant on SofaHelper and
# SofaDefaultType targets, which are defined there.
if(NOT SOFA_NO_OPENGL)
    find_package(Sofa.GL CONFIG QUIET REQUIRED)
endif()

check_required_components(@PROJECT_NAME@)

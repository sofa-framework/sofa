# CMake package configuration file for SofaFramework

@PACKAGE_INIT@

list(FIND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}" HAS_SOFAFRAMEWORK_CMAKE_MODULE_PATH)
if(HAS_SOFAFRAMEWORK_CMAKE_MODULE_PATH EQUAL -1)
    list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})
endif()

list(FIND CMAKE_PREFIX_PATH "${CMAKE_CURRENT_LIST_DIR}/../../../plugins" HAS_PLUGINS_CMAKE_PREFIX_PATH)
if(HAS_PLUGINS_CMAKE_PREFIX_PATH EQUAL -1)
    list(APPEND CMAKE_PREFIX_PATH "${CMAKE_CURRENT_LIST_DIR}/../../../plugins")
endif()

include(SofaMacros)

# Help finding headers (install only)
# TODO: a better way to do this?
set(INCLUDE_PATHS
    "${CMAKE_CURRENT_LIST_DIR}/../../../include/extlibs/WinDepPack"
    "${CMAKE_CURRENT_LIST_DIR}/../../../include/extlibs/Eigen"
    )
foreach(include_path ${INCLUDE_PATHS})
    list(FIND CMAKE_INCLUDE_PATH ${include_path} found_index)
    if(${found_index} EQUAL -1)
        list(APPEND CMAKE_INCLUDE_PATH ${include_path})
    endif()
endforeach()

find_package(TinyXML REQUIRED)

set(SOFA_HAVE_ZLIB "@SOFA_HAVE_ZLIB@")
set(SOFA_HAVE_GLEW "@SOFA_HAVE_GLEW@")
set(SOFA_HAVE_BOOST "@SOFA_HAVE_BOOST@")
set(SOFA_HAVE_GTEST "@SOFA_HAVE_GTEST@")

set(SOFA_BUILD_METIS "@SOFA_BUILD_METIS@")

set(SOFA_NO_OPENGL "@SOFA_NO_OPENGL@")

set(SOFA_USE_MASK "@SOFA_USE_MASK@")

set(SOFA_WITH_DEVTOOLS "@SOFA_WITH_DEVTOOLS@")
set(SOFA_WITH_THREADING "@SOFA_WITH_THREADING@")
set(SOFA_WITH_DEPRECATED_COMPONENTS "@SOFA_WITH_DEPRECATED_COMPONENTS@")

if(SOFA_HAVE_GTEST)
    find_package(GTest CONFIG REQUIRED)
endif()

if(SOFA_HAVE_ZLIB)
    find_package(ZLIB REQUIRED)
endif()

if(SOFA_HAVE_GLEW)
    find_package(GLEW REQUIRED)
endif()

if(SOFA_HAVE_BOOST)
    find_package(Boost QUIET REQUIRED system filesystem locale program_options OPTIONAL_COMPONENTS date_time thread)
endif()

if(NOT TARGET SofaCore)
    include("${CMAKE_CURRENT_LIST_DIR}/SofaFrameworkTargets.cmake")
endif()

check_required_components(SofaHelper SofaDefaultType SofaCore)
